dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.53)

AC_INIT([poedit], [1.2.4], [vaclav.slavik@matfyz.cz])

AC_CONFIG_SRCDIR([README])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl cross-compiling support: we're cross compiling if the build system is
dnl different from the target one (assume host and target be always the same)
if test "$build" != "$host" ; then
    CC=$host_alias-gcc
    CXX=$host_alias-c++
    AR=$host_alias-ar
    RANLIB=$host_alias-ranlib
    DLLTOOL=$host_alias-dlltool
    RESCOMP=$host_alias-windres
    LD=$host_alias-ld
    NM=$host_alias-nm
    STRIP=$host_alias-strip
else
    RESCOMP=windres
fi

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

AM_OPTIONS_WXCONFIG

AC_ARG_ENABLE(transmem, [  --enable-transmem       Enable translation memory (default: yes)],
              USE_TRANSMEM="$enableval", USE_TRANSMEM="yes")
AC_ARG_ENABLE(debug, [  --enable-debug          Enable debugging information],
              USE_DEBUG="$enableval", USE_DEBUG="no")
AC_ARG_ENABLE(semistatic, [  --enable-semistatic     Link statically against wxWindows and libdb],
              USE_SEMISTATIC="$enableval", USE_SEMISTATIC="no")

AC_MSG_CHECKING(for install location)
case "$prefix" in
NONE)	AC_CACHE_VAL(m_cv_prefix,m_cv_prefix=$ac_default_prefix);;
*)	m_cv_prefix=$prefix ;;
esac
AC_MSG_RESULT($m_cv_prefix)
case "$m_cv_prefix" in /*) ;; *)
	AC_MSG_WARN([--prefix=$prefix must be an absolute path name, using $ac_default_prefix])
	m_cv_prefix=$ac_default_prefix
esac
prefix=$m_cv_prefix


dnl Checks for programs.
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP


AC_ARG_VAR(ZIP, Path to zip executable)
AC_PATH_PROG(ZIP, zip, [])
if test x$ZIP = x ; then
    AC_MSG_ERROR([ZIP archiver is needed to compile poEdit.])
fi



case "$USE_DEBUG" in
yes)	DEBUG_FLAGS="-g -Wall"
	WX_CONFIG_NAME="wxgtkd-2.3-config"
    ;;    
*)	DEBUG_FLAGS="-O3"
    ;;
esac


AM_PATH_WXCONFIG(2.4.1, WXFOUND=1)

if test "$WXFOUND" != 1; then
    AC_MSG_ERROR([
        Please check that wx-config is in path, the directory
        where wxWindows libraries are installed (returned by
        'wx-config --libs' command) is in LD_LIBRARY_PATH or
        equivalent variable and wxWindows is version 2.4.0 or above.
    ])
fi

if $WX_CONFIG_NAME --cxxflags | grep -q WXMSW; then
    use_windows=yes
    if test x$USE_TRANSMEM != xno ; then
        CXXFLAGS="$CXXFLAGS -DUSE_TRANSMEM -DDB_HEADER=\\\"db.h\\\" -I\$(top_srcdir)/extras/win32-db3"
    fi
    
    changequote(<<,>>)
    RC_WX_INCLUDES=`echo " $WX_CPPFLAGS" | sed -e 's/ -[^I][^ ]*//g' | sed -e 's/ -I\([^ ]*\)/ --include-dir \1/g'`
    changequote([,])

    AM_CONDITIONAL(USE_KDE, test 0 = 1)
    AM_CONDITIONAL(USE_GNOME, test 0 = 1)
else
    use_windows=no
    FIND_KDE
    FIND_GNOME
    if test x$USE_TRANSMEM != xno ; then
        FIND_BERKELEY_DB
    fi
fi
AM_CONDITIONAL(USE_WINDOWS, test $use_windows = yes)

CATALOGS_TO_INSTALL="install-poedit-catalogs"

if test x$USE_SEMISTATIC = xyes ; then
    CATALOGS_TO_INSTALL="$CATALOGS_TO_INSTALL install-wxstd-catalogs"

    changequote(<<,>>)

    dnl link libdb3 statically:
    dnl (FIXME - this only works with gcc and gnu ld!)
    LIBS=`echo $LIBS | sed -e 's/\(-ldb[^ ]*\)/-Wl,-Bstatic \1 -Wl,-Bdynamic/g'`

    dnl ...and don't link libtiff, libjpeg, because we don't use them:
    POEDIT_WX_LIBS=`echo $WX_LIBS_STATIC | sed -e 's/\(-ltiff\|-ljpeg\)//g'`

    changequote([,])
else
    POEDIT_WX_LIBS="$WX_LIBS"
fi

CXXFLAGS="$CXXFLAGS $DEBUG_FLAGS $WX_CXXFLAGS \"-DPOEDIT_PREFIX=\\\"$prefix\\\"\""

AC_SUBST(LDFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(RESCOMP)
AC_SUBST(WX_CONFIG_NAME)
AC_SUBST(POEDIT_WX_LIBS)
AC_SUBST(RC_WX_INCLUDES)
AC_SUBST(CATALOGS_TO_INSTALL)

AC_CONFIG_FILES([
         Makefile
         extras/Makefile
         extras/wx-contrib/Makefile
         extras/wx-contrib/src/Makefile
         extras/wx-contrib/src/gizmos/Makefile
         extras/wx-contrib/src/xrc/Makefile
         extras/wx-contrib/src/xrc/expat/Makefile
         src/Makefile
         src/resources/Makefile
         locales/Makefile
         docs/Makefile
         docs/en/Makefile
         docs/hr/Makefile
         install/Makefile
         install/poedit.keys
         ])

AC_OUTPUT
