dnl Process this file with autoconf to produce a configure script.
AC_INIT(aclocal.m4)

AC_CANONICAL_SYSTEM

dnl cross-compiling support: we're cross compiling if the build system is
dnl different from the target one (assume host and target be always the same)
if test "$build" != "$host" ; then
    CC=$host_alias-gcc
    CXX=$host_alias-c++
    AR=$host_alias-ar
    RANLIB=$host_alias-ranlib
    DLLTOOL=$host_alias-dlltool
    RESCOMP=$host_alias-windres
    LD=$host_alias-ld
    NM=$host_alias-nm
    STRIP=$host_alias-strip
fi

AM_INIT_AUTOMAKE(poedit, '1.1.4', avoid_defining_package_and_version)

AC_ARG_ENABLE(transmem, [  --enable-transmem       Enable translation memory (default: yes)],
              USE_TRANSMEM="$enableval", USE_TRANSMEM="yes")
AC_ARG_ENABLE(debug, [  --enable-debug          Enable debugging information],
              USE_DEBUG="$enableval", USE_DEBUG="no")


AC_MSG_CHECKING(for install location)
case "$prefix" in
NONE)	AC_CACHE_VAL(m_cv_prefix,m_cv_prefix=$ac_default_prefix);;
*)	m_cv_prefix=$prefix ;;
esac
AC_MSG_RESULT($m_cv_prefix)
case "$m_cv_prefix" in /*) ;; *)
	AC_MSG_WARN([--prefix=$prefix must be an absolute path name, using $ac_default_prefix])
	m_cv_prefix=$ac_default_prefix
esac
prefix=$m_cv_prefix


dnl Checks for programs.
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP




case "$USE_DEBUG" in
yes)	DEBUG_FLAGS="-g -Wall"
	WX_CONFIG="wxgtkd-config"
    ;;    
*)	DEBUG_FLAGS="-O3"
    ;;
esac



WX_PATH_WXCONFIG(2.3.0, WXFOUND=1)
if test "$WXFOUND" != 1; then
    AC_MSG_ERROR([
        Please check that wx-config is in path, the directory
        where wxWindows libraries are installed (returned by
        'wx-config --libs' command) is in LD_LIBRARY_PATH or
        equivalent variable and wxWindows is version 2.3.0 or above.
    ])
fi

if $WX_CONFIG --cxxflags | grep -q WXMSW; then
    AM_CONDITIONAL(USE_WINDOWS, test x = x)
    EXE_SUFFIX=.exe
    if test x$USE_TRANSMEM != xno ; then
        CXXFLAGS="$CXXFLAGS -DUSE_TRANSMEM -I\$(top_srcdir)/extras/win32-db3"
    fi
else
    FIND_KDE
    FIND_GNOME
    if test x$USE_TRANSMEM != xno ; then
        FIND_BERKELEY_DB
    fi
fi

CXXFLAGS="$CXXFLAGS $DEBUG_FLAGS $WX_CFLAGS \"-DPOEDIT_PREFIX=\\\"$prefix\\\"\""

AC_SUBST(WX_CONFIG)
AC_SUBST(WX_LIBS)
AC_SUBST(LDFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(RESCOMP)
AC_SUBST(EXE_SUFFIX)


AC_OUTPUT([
         Makefile
         extras/Makefile
         extras/wx-contrib/Makefile
         extras/wx-contrib/src/Makefile
         extras/wx-contrib/src/gizmos/Makefile
         extras/wx-contrib/src/xrc/Makefile
         extras/wx-contrib/src/xrc/expat/Makefile
         src/Makefile
         src/resources/Makefile
         docs/Makefile
         install/Makefile
         install/poedit.keys
         ])

